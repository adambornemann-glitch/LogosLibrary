/-
Copyright (c) 2026 Adam Bornemann. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Adam Bornemann
-/
import LogosLibrary.QuantumMechanics.UnitaryEvo.Bochner.Limits
import Mathlib.Probability.Distributions.Gaussian.Real

/-!
# Generator Domain and Self-Adjointness

This file constructs the generator of a strongly continuous one-parameter unitary
group and proves it is self-adjoint.

## Main definitions

* `generatorDomain`: the submodule of vectors where the generator limit exists
* `generatorOp`: the generator as a linear map on its domain
* `generatorOfUnitaryGroup`: the complete `Generator` structure
* `averagedVector`: time-averaged vectors used to prove domain density

## Main results

* `generatorDomain_dense`: the generator domain is dense in `H`
* `range_plus_i_eq_top`: `ran(A + iI) = H`
* `range_minus_i_eq_top`: `ran(A - iI) = H`
* `generatorOfUnitaryGroup_isSelfAdjoint`: the generator is self-adjoint

## Implementation notes

Self-adjointness is proved using the criterion: `A` is self-adjoint iff `A` is
symmetric and `ran(A Â± iI) = H`. This avoids dealing with the adjoint of an
unbounded operator directly.

Domain density uses averaged vectors: `hâ»Â¹ âˆ«â‚€Ê° U(t)Ï† dt â†’ Ï†` as `h â†’ 0`,
and these averaged vectors lie in the domain.

## Tags

generator, self-adjoint, domain, Stone's theorem
-/

namespace QuantumMechanics.Bochner

open MeasureTheory Measure Filter Topology Complex QuantumMechanics.Generators

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [CompleteSpace H]

set_option linter.unusedSectionVars false

section GeneratorConstruction

open Classical
open InnerProductSpace
variable (U_grp : OneParameterUnitaryGroup (H := H))

/-- The domain of the generator: vectors where the limit `lim_{hâ†’0} (U(h)Ïˆ - Ïˆ)/(ih)` exists. -/
noncomputable def generatorDomain : Submodule â„‚ H where
  carrier := {Ïˆ : H | âˆƒ (Î· : H), Tendsto (fun h : â„ => ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆ - Ïˆ))
                                         (ğ“[â‰ ] 0) (ğ“ Î·)}
  add_mem' := by
    intro Ïˆâ‚ Ïˆâ‚‚ âŸ¨Î·â‚, hÎ·â‚âŸ© âŸ¨Î·â‚‚, hÎ·â‚‚âŸ©
    refine âŸ¨Î·â‚ + Î·â‚‚, ?_âŸ©
    have h_add : âˆ€ h : â„, ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (Ïˆâ‚ + Ïˆâ‚‚) - (Ïˆâ‚ + Ïˆâ‚‚)) =
                         ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚ - Ïˆâ‚) +
                         ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚‚ - Ïˆâ‚‚) := by
      intro h
      rw [map_add]
      ring_nf
      rw [â† smul_add]
      congr 1
      abel
    simp_rw [h_add]
    exact hÎ·â‚.add hÎ·â‚‚
  zero_mem' := by
    refine âŸ¨0, ?_âŸ©
    simp only [map_zero, sub_zero, smul_zero]
    exact tendsto_const_nhds
  smul_mem' := by
    intro c Ïˆ âŸ¨Î·, hÎ·âŸ©
    refine âŸ¨c â€¢ Î·, ?_âŸ©
    have h_smul : âˆ€ h : â„, ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (c â€¢ Ïˆ) - c â€¢ Ïˆ) =
                          c â€¢ (((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆ - Ïˆ)) := by
      intro h
      rw [ContinuousLinearMap.map_smul, smul_sub, smul_comm]
      rw [smul_comm ((I * (h : â„‚))â»Â¹) c Ïˆ, â† smul_sub, â† smul_sub]
    simp_rw [h_smul]
    exact hÎ·.const_smul c

noncomputable def generatorLimitValue (Ïˆ : H)
    (hÏˆ : Ïˆ âˆˆ generatorDomain U_grp) : H :=
  Classical.choose hÏˆ

lemma generatorLimitValue_spec (Ïˆ : H) (hÏˆ : Ïˆ âˆˆ generatorDomain U_grp) :
    Tendsto (fun h : â„ => ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆ - Ïˆ))
            (ğ“[â‰ ] 0) (ğ“ (generatorLimitValue U_grp Ïˆ hÏˆ)) :=
  Classical.choose_spec hÏˆ

/-- The generator of a unitary group as a linear map on its domain. -/
noncomputable def generatorOp : (generatorDomain U_grp) â†’â‚—[â„‚] H where
  toFun := fun âŸ¨Ïˆ, hÏˆâŸ© => generatorLimitValue U_grp Ïˆ hÏˆ
  map_add' := by
    intro âŸ¨Ïˆâ‚, hÏˆâ‚âŸ© âŸ¨Ïˆâ‚‚, hÏˆâ‚‚âŸ©
    have hÏˆ_sum : Ïˆâ‚ + Ïˆâ‚‚ âˆˆ generatorDomain U_grp := (generatorDomain U_grp).add_mem hÏˆâ‚ hÏˆâ‚‚
    simp
    have hâ‚ := generatorLimitValue_spec U_grp Ïˆâ‚ hÏˆâ‚
    have hâ‚‚ := generatorLimitValue_spec U_grp Ïˆâ‚‚ hÏˆâ‚‚
    have h_sum := generatorLimitValue_spec U_grp (Ïˆâ‚ + Ïˆâ‚‚) hÏˆ_sum
    have h_add_lim : Tendsto (fun h : â„ => ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (Ïˆâ‚ + Ïˆâ‚‚) - (Ïˆâ‚ + Ïˆâ‚‚)))
                             (ğ“[â‰ ] 0)
                             (ğ“ (generatorLimitValue U_grp Ïˆâ‚ hÏˆâ‚ + generatorLimitValue U_grp Ïˆâ‚‚ hÏˆâ‚‚)) := by
      have h_eq : âˆ€ h : â„, ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (Ïˆâ‚ + Ïˆâ‚‚) - (Ïˆâ‚ + Ïˆâ‚‚)) =
                          ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚ - Ïˆâ‚) +
                          ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚‚ - Ïˆâ‚‚) := by
        intro h
        rw [map_add, â† smul_add]
        congr 1
        abel
      simp_rw [h_eq]
      exact hâ‚.add hâ‚‚
    exact tendsto_nhds_unique h_sum h_add_lim
  map_smul' := by
    intro c âŸ¨Ïˆ, hÏˆâŸ©
    have hcÏˆ : c â€¢ Ïˆ âˆˆ generatorDomain U_grp := (generatorDomain U_grp).smul_mem c hÏˆ
    simp only [RingHom.id_apply]
    have h := generatorLimitValue_spec U_grp Ïˆ hÏˆ
    have hc := generatorLimitValue_spec U_grp (c â€¢ Ïˆ) hcÏˆ
    have h_smul_lim : Tendsto (fun h : â„ => ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (c â€¢ Ïˆ) - c â€¢ Ïˆ))
                              (ğ“[â‰ ] 0)
                              (ğ“ (c â€¢ generatorLimitValue U_grp Ïˆ hÏˆ)) := by
      have h_eq : âˆ€ h : â„, ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (c â€¢ Ïˆ) - c â€¢ Ïˆ) =
                          c â€¢ (((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆ - Ïˆ)) := by
        intro h
        rw [ContinuousLinearMap.map_smul, smul_sub, smul_comm]
        rw [smul_comm ((I * (h : â„‚))â»Â¹) c Ïˆ, â† smul_sub]
        congr 1
        rw [â† smul_sub]
      simp_rw [h_eq]
      exact h.const_smul c
    exact tendsto_nhds_unique hc h_smul_lim

theorem generator_formula_holds (Ïˆ : generatorDomain U_grp) :
    Tendsto (fun h : â„ => ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (Ïˆ : H) - (Ïˆ : H)))
            (ğ“[â‰ ] 0)
            (ğ“ (generatorOp U_grp Ïˆ)) := by
  exact generatorLimitValue_spec U_grp Ïˆ.val Ïˆ.property

theorem generatorDomain_invariant (t : â„) (Ïˆ : H) (hÏˆ : Ïˆ âˆˆ generatorDomain U_grp) :
    U_grp.U t Ïˆ âˆˆ generatorDomain U_grp := by
  obtain âŸ¨Î·, hÎ·âŸ© := hÏˆ
  refine âŸ¨U_grp.U t Î·, ?_âŸ©
  have h_eq : âˆ€ h : â„, ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h (U_grp.U t Ïˆ) - U_grp.U t Ïˆ) =
                       U_grp.U t (((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆ - Ïˆ)) := by
    intro h
    have h_comm : U_grp.U h (U_grp.U t Ïˆ) = U_grp.U t (U_grp.U h Ïˆ) := by
      calc U_grp.U h (U_grp.U t Ïˆ)
          = (U_grp.U h).comp (U_grp.U t) Ïˆ := rfl
        _ = U_grp.U (h + t) Ïˆ := by rw [â† U_grp.group_law]
        _ = U_grp.U (t + h) Ïˆ := by rw [add_comm]
        _ = (U_grp.U t).comp (U_grp.U h) Ïˆ := by rw [U_grp.group_law]
        _ = U_grp.U t (U_grp.U h Ïˆ) := rfl
    rw [h_comm, â† ContinuousLinearMap.map_sub, ContinuousLinearMap.map_smul]
  simp_rw [h_eq]
  exact (U_grp.U t).continuous.tendsto _ |>.comp hÎ·

theorem generator_symmetric (Ïˆâ‚ Ïˆâ‚‚ : generatorDomain U_grp) :
    âŸªgeneratorOp U_grp Ïˆâ‚, (Ïˆâ‚‚ : H)âŸ«_â„‚ = âŸª(Ïˆâ‚ : H), generatorOp U_grp Ïˆâ‚‚âŸ«_â„‚ := by
  have hâ‚ := generatorLimitValue_spec U_grp Ïˆâ‚.val Ïˆâ‚.property
  have hâ‚‚ := generatorLimitValue_spec U_grp Ïˆâ‚‚.val Ïˆâ‚‚.property
  have h_lhs : Tendsto (fun h : â„ => âŸª((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚ - Ïˆâ‚), (Ïˆâ‚‚ : H)âŸ«_â„‚)
                       (ğ“[â‰ ] 0) (ğ“ âŸªgeneratorOp U_grp Ïˆâ‚, (Ïˆâ‚‚ : H)âŸ«_â„‚) :=
    Tendsto.inner hâ‚ tendsto_const_nhds
  have h_rhs : Tendsto (fun h : â„ => âŸª(Ïˆâ‚ : H), ((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚‚ - Ïˆâ‚‚)âŸ«_â„‚)
                       (ğ“[â‰ ] 0) (ğ“ âŸª(Ïˆâ‚ : H), generatorOp U_grp Ïˆâ‚‚âŸ«_â„‚) :=
    Tendsto.inner tendsto_const_nhds hâ‚‚
  have h_eq : âˆ€ h : â„, h â‰  0 â†’
      âŸª((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ïˆâ‚ - Ïˆâ‚), (Ïˆâ‚‚ : H)âŸ«_â„‚ =
      âŸª(Ïˆâ‚ : H), ((I * (-h))â»Â¹ : â„‚) â€¢ (U_grp.U (-h) Ïˆâ‚‚ - Ïˆâ‚‚)âŸ«_â„‚ := by
    intro h hh
    rw [inner_smul_left]
    have h_unitary : âŸªU_grp.U h Ïˆâ‚, (Ïˆâ‚‚ : H)âŸ«_â„‚ = âŸª(Ïˆâ‚ : H), U_grp.U (-h) Ïˆâ‚‚âŸ«_â„‚ := by
      calc âŸªU_grp.U h Ïˆâ‚, (Ïˆâ‚‚ : H)âŸ«_â„‚
          = âŸªU_grp.U (-h) (U_grp.U h Ïˆâ‚), U_grp.U (-h) Ïˆâ‚‚âŸ«_â„‚ := by rw [U_grp.unitary (-h)]
        _ = âŸª(U_grp.U (-h)).comp (U_grp.U h) Ïˆâ‚, U_grp.U (-h) Ïˆâ‚‚âŸ«_â„‚ := rfl
        _ = âŸªU_grp.U ((-h) + h) Ïˆâ‚, U_grp.U (-h) Ïˆâ‚‚âŸ«_â„‚ := by rw [â† U_grp.group_law]
        _ = âŸªU_grp.U 0 Ïˆâ‚, U_grp.U (-h) Ïˆâ‚‚âŸ«_â„‚ := by ring_nf
        _ = âŸª(Ïˆâ‚ : H), U_grp.U (-h) Ïˆâ‚‚âŸ«_â„‚ := by rw [U_grp.identity]; rfl
    rw [inner_sub_left, h_unitary, â† inner_sub_right]
    rw [inner_smul_right]
    congr 1
    simp only [map_invâ‚€, map_mul, Complex.conj_I, Complex.conj_ofReal]
    ring
  have h_rhs' : Tendsto (fun h : â„ => âŸª(Ïˆâ‚ : H), ((I * (-h))â»Â¹ : â„‚) â€¢ (U_grp.U (-h) Ïˆâ‚‚ - Ïˆâ‚‚)âŸ«_â„‚)
                        (ğ“[â‰ ] 0) (ğ“ âŸª(Ïˆâ‚ : H), generatorOp U_grp Ïˆâ‚‚âŸ«_â„‚) := by
    have h_neg : Tendsto (fun h : â„ => -h) (ğ“[â‰ ] 0) (ğ“[â‰ ] 0) := by
      rw [tendsto_nhdsWithin_iff]
      constructor
      Â· have : Tendsto (fun h : â„ => -h) (ğ“ 0) (ğ“ 0) := by
          convert (continuous_neg (G := â„)).tendsto 0 using 1
          simp
        exact this.mono_left nhdsWithin_le_nhds
      Â· filter_upwards [self_mem_nhdsWithin] with h hh
        simp only [Set.mem_compl_iff, Set.mem_singleton_iff, neg_eq_zero]
        exact hh
    have h_comp := h_rhs.comp h_neg
    apply Tendsto.congr _ h_comp
    intro h
    simp only [Function.comp_apply, ofReal_neg]
  refine tendsto_nhds_unique ?_ h_rhs'
  apply Tendsto.congr' _ h_lhs
  filter_upwards [self_mem_nhdsWithin] with h hh
  exact h_eq h hh

theorem resolventIntegralPlus_in_domain (Ï† : H) :
    resolventIntegralPlus U_grp Ï† âˆˆ generatorDomain U_grp := by
  exact âŸ¨Ï† - I â€¢ resolventIntegralPlus U_grp Ï†, generator_limit_resolventIntegralPlus U_grp Ï†âŸ©

theorem resolventIntegralMinus_in_domain (Ï† : H) :
    resolventIntegralMinus U_grp Ï† âˆˆ generatorDomain U_grp := by
  exact âŸ¨Ï† + I â€¢ resolventIntegralMinus U_grp Ï†, generator_limit_resolventIntegralMinus U_grp Ï†âŸ©

theorem resolventIntegralPlus_solves (Ï† : H) :
    generatorOp U_grp âŸ¨resolventIntegralPlus U_grp Ï†, resolventIntegralPlus_in_domain U_grp Ï†âŸ© +
    I â€¢ resolventIntegralPlus U_grp Ï† = Ï† := by
      classical
  have hÏˆ := resolventIntegralPlus_in_domain U_grp Ï†
  simp only [generatorOp]
  have h_lim := generatorLimitValue_spec U_grp (resolventIntegralPlus U_grp Ï†) hÏˆ
  have h_target := generator_limit_resolventIntegralPlus U_grp Ï†
  have h_eq := tendsto_nhds_unique h_lim h_target
  abel_nf
  rw [@LinearMap.coe_mk]
  simp_all only [mul_inv_rev, inv_I, mul_neg, neg_smul, AddHom.coe_mk, sub_add_cancel]

theorem resolventIntegralMinus_solves (Ï† : H) :
    generatorOp U_grp âŸ¨resolventIntegralMinus U_grp Ï†, resolventIntegralMinus_in_domain U_grp Ï†âŸ© -
    I â€¢ resolventIntegralMinus U_grp Ï† = Ï† := by
  classical
  have hÏˆ := resolventIntegralMinus_in_domain U_grp Ï†
  simp only [generatorOp]
  have h_lim := generatorLimitValue_spec U_grp (resolventIntegralMinus U_grp Ï†) hÏˆ
  have h_target := generator_limit_resolventIntegralMinus U_grp Ï†
  have h_eq := tendsto_nhds_unique h_lim h_target
  abel_nf
  simp_all only [mul_inv_rev, inv_I, mul_neg, neg_smul, LinearMap.coe_mk, AddHom.coe_mk, Int.reduceNeg,
    one_smul, add_neg_cancel_right]

theorem range_plus_i_eq_top :
    âˆ€ Ï† : H, âˆƒ Ïˆ : generatorDomain U_grp,
      generatorOp U_grp Ïˆ + I â€¢ (Ïˆ : H) = Ï† := by
  intro Ï†
  exact âŸ¨âŸ¨resolventIntegralPlus U_grp Ï†, resolventIntegralPlus_in_domain U_grp Ï†âŸ©,
         resolventIntegralPlus_solves U_grp Ï†âŸ©

theorem range_minus_i_eq_top :
    âˆ€ Ï† : H, âˆƒ Ïˆ : generatorDomain U_grp,
      generatorOp U_grp Ïˆ - I â€¢ (Ïˆ : H) = Ï† := by
  intro Ï†
  exact âŸ¨âŸ¨resolventIntegralMinus U_grp Ï†, resolventIntegralMinus_in_domain U_grp Ï†âŸ©,
         resolventIntegralMinus_solves U_grp Ï†âŸ©

end GeneratorConstruction

section AveragedVectors

variable (U_grp : OneParameterUnitaryGroup (H := H))

/-- Time-averaged vector: `hâ»Â¹ âˆ«â‚€Ê° U(t)Ï† dt`. These lie in the generator domain
    and converge to `Ï†` as `h â†’ 0`, proving domain density. -/
noncomputable def averagedVector (h : â„) (_ : h â‰  0) (Ï† : H) : H :=
  (hâ»Â¹ : â„‚) â€¢ âˆ« t in Set.Ioc 0 h, U_grp.U t Ï†

lemma averagedVector_tendsto (Ï† : H) :
    Tendsto (fun h : â„ => if hh : h â‰  0 then averagedVector U_grp h hh Ï† else Ï†)
            (ğ“[>] 0) (ğ“ Ï†) := by
  unfold averagedVector
  have h_cont : Continuous (fun t => U_grp.U t Ï†) := U_grp.strong_continuous Ï†
  have h_f0 : U_grp.U 0 Ï† = Ï† := by rw [U_grp.identity]; rfl
  have h_deriv : HasDerivAt (fun x => âˆ« t in (0 : â„)..x, U_grp.U t Ï†) (U_grp.U 0 Ï†) 0 := by
    apply intervalIntegral.integral_hasDerivAt_right
    Â· exact h_cont.intervalIntegrable 0 0
    Â· exact Continuous.stronglyMeasurableAtFilter h_cont volume (ğ“ 0)
    Â· exact h_cont.continuousAt
  rw [h_f0] at h_deriv
  have h_F0 : âˆ« t in (0 : â„)..0, U_grp.U t Ï† = 0 := intervalIntegral.integral_same
  have h_tendsto_real : Tendsto (fun h : â„ => hâ»Â¹ â€¢ âˆ« t in (0 : â„)..h, U_grp.U t Ï†)
                                (ğ“[â‰ ] 0) (ğ“ Ï†) := by
    have := h_deriv.hasDerivWithinAt (s := Set.univ \ {0})
    rw [hasDerivWithinAt_iff_tendsto_slope] at this
    simp only [Set.diff_diff, Set.union_self] at this
    convert this using 1
    Â· ext h
      unfold slope
      simp only [sub_zero, h_F0, vsub_eq_sub]
    Â· congr 1
      exact Set.compl_eq_univ_diff {(0 : â„)}
  have h_restrict := h_tendsto_real.mono_left (nhdsWithin_mono 0 (fun x hx => ne_of_gt hx))
  apply Tendsto.congr' _ h_restrict
  filter_upwards [self_mem_nhdsWithin] with h hh
  rw [dif_pos (ne_of_gt hh)]
  rw [intervalIntegral.integral_of_le (le_of_lt hh)]
  rw [(Complex.coe_smul hâ»Â¹ _).symm, ofReal_inv]

lemma averagedVector_in_domain (h : â„) (hh : h â‰  0) (Ï† : H) :
    averagedVector U_grp h hh Ï† âˆˆ generatorDomain U_grp := by
  by_cases hpos : h > 0
  Â· refine âŸ¨((I * h)â»Â¹ : â„‚) â€¢ (U_grp.U h Ï† - Ï†), ?_âŸ©
    have h_cont : Continuous (fun t => U_grp.U t Ï†) := U_grp.strong_continuous Ï†
    set Ïˆ := averagedVector U_grp h hh Ï† with hÏˆ_def
    have h_FTC1 : Tendsto (fun s : â„ => (sâ»Â¹ : â„‚) â€¢ âˆ« t in (0 : â„)..s, U_grp.U t Ï†) (ğ“[â‰ ] 0) (ğ“ Ï†) := by
      have h_deriv : HasDerivAt (fun x => âˆ« t in (0 : â„)..x, U_grp.U t Ï†) Ï† 0 := by
        have := intervalIntegral.integral_hasDerivAt_right (h_cont.intervalIntegrable 0 0)
                  (h_cont.stronglyMeasurableAtFilter volume (ğ“ 0)) h_cont.continuousAt
        simp only [U_grp.identity, ContinuousLinearMap.id_apply] at this
        exact this
      have h_F0 : âˆ« t in (0 : â„)..0, U_grp.U t Ï† = 0 := intervalIntegral.integral_same
      rw [hasDerivAt_iff_tendsto_slope] at h_deriv
      apply h_deriv.congr'
      filter_upwards [self_mem_nhdsWithin] with s hs
      unfold slope
      simp only [vsub_eq_sub, sub_zero, h_F0, sub_zero]
      rw [(Complex.coe_smul sâ»Â¹ _).symm, ofReal_inv]
    have h_FTC2 : Tendsto (fun s : â„ => (sâ»Â¹ : â„‚) â€¢ âˆ« t in (h : â„)..(h + s), U_grp.U t Ï†) (ğ“[â‰ ] 0) (ğ“ (U_grp.U h Ï†)) := by
      have h_deriv : HasDerivAt (fun x => âˆ« t in (h : â„)..x, U_grp.U t Ï†) (U_grp.U h Ï†) h := by
        exact intervalIntegral.integral_hasDerivAt_right (h_cont.intervalIntegrable h h)
                (h_cont.stronglyMeasurableAtFilter volume (ğ“ h)) h_cont.continuousAt
      have h_Fh : âˆ« t in (h : â„)..h, U_grp.U t Ï† = 0 := intervalIntegral.integral_same
      rw [hasDerivAt_iff_tendsto_slope] at h_deriv
      have h_shift : Tendsto (fun s : â„ => h + s) (ğ“[â‰ ] 0) (ğ“[â‰ ] h) := by
        rw [tendsto_nhdsWithin_iff]
        constructor
        Â· have : Tendsto (fun s : â„ => h + s) (ğ“ 0) (ğ“ h) := by
            have h1 : Tendsto (fun _ : â„ => h) (ğ“ 0) (ğ“ h) := tendsto_const_nhds
            have h2 : Tendsto (fun s : â„ => s) (ğ“ 0) (ğ“ 0) := tendsto_id
            convert h1.add h2 using 1
            simp only [add_zero]
          exact this.mono_left nhdsWithin_le_nhds
        Â· filter_upwards [self_mem_nhdsWithin] with s hs
          simp only [Set.mem_compl_iff, Set.mem_singleton_iff, add_eq_left]
          exact hs
      have := h_deriv.comp h_shift
      simp only at this
      apply this.congr'
      filter_upwards [self_mem_nhdsWithin] with s hs
      unfold slope
      simp only [vsub_eq_sub, h_Fh, sub_zero, Function.comp_apply, add_sub_cancel_left]
      rw [(Complex.coe_smul sâ»Â¹ _).symm, ofReal_inv]
    have h_key : âˆ€ s : â„, s â‰  0 â†’
        ((I * s)â»Â¹ : â„‚) â€¢ (U_grp.U s Ïˆ - Ïˆ) =
        ((I * h)â»Â¹ : â„‚) â€¢ (((sâ»Â¹ : â„‚) â€¢ âˆ« t in (h : â„)..(h + s), U_grp.U t Ï†) -
                           ((sâ»Â¹ : â„‚) â€¢ âˆ« t in (0 : â„)..s, U_grp.U t Ï†)) := by
      intro s hs
      rw [hÏˆ_def]
      unfold averagedVector
      rw [ContinuousLinearMap.map_smul]
      have h_shift_int : U_grp.U s (âˆ« t in Set.Ioc 0 h, U_grp.U t Ï†) =
                         âˆ« t in Set.Ioc s (s + h), U_grp.U t Ï† := by
        rw [â† (U_grp.U s).integral_comp_comm h_cont.integrableOn_Ioc]
        have h_subst : âˆ« t in Set.Ioc 0 h, U_grp.U s (U_grp.U t Ï†) =
                       âˆ« t in Set.Ioc 0 h, U_grp.U (s + t) Ï† := by
          congr 1; ext t
          rw [@OneParameterUnitaryGroup.group_law]
          rfl
        rw [h_subst]
        have h_preimage : (fun t => t - s) â»Â¹' (Set.Ioc 0 h) = Set.Ioc s (s + h) := by
          ext t; simp only [Set.mem_preimage, Set.mem_Ioc]; constructor <;> intro âŸ¨a, bâŸ© <;> constructor <;> linarith
        have h_meas : Measure.map (fun t => t - s) volume = volume :=
          (measurePreserving_sub_right volume s).map_eq
        rw [â† h_meas, MeasureTheory.setIntegral_map measurableSet_Ioc]
        Â· simp only [h_preimage]; congr 1
          exact congrFun (congrArg restrict (id (Eq.symm h_meas))) (Set.Ioc s (s + h))
          simp only [add_sub_cancel]
        Â· exact h_cont.aestronglyMeasurable.comp_measurable (measurable_const_add s)
        Â· exact (measurable_sub_const s).aemeasurable
      rw [h_shift_int]
      rw [â† smul_sub, smul_smul]
      have h_Ioc_eq_interval : âˆ€ a b : â„, a â‰¤ b â†’ âˆ« t in Set.Ioc a b, U_grp.U t Ï† =
                                                    âˆ« t in a..b, U_grp.U t Ï† := by
        intro a b hab
        rw [intervalIntegral.integral_of_le hab]
      rw [h_Ioc_eq_interval s (s + h) (by linarith), h_Ioc_eq_interval 0 h (le_of_lt hpos)]
      have h_arith : (âˆ« t in s..(s + h), U_grp.U t Ï†) - âˆ« t in (0 : â„)..h, U_grp.U t Ï† =
               (âˆ« t in (h : â„)..(h + s), U_grp.U t Ï†) - âˆ« t in (0 : â„)..s, U_grp.U t Ï† := by
        have hint : âˆ€ a b : â„, IntervalIntegrable (fun t => U_grp.U t Ï†) volume a b :=
          fun a b => h_cont.intervalIntegrable a b
        have h3 : s + h = h + s := add_comm s h
        have key : (âˆ« t in s..(s + h), U_grp.U t Ï†) + âˆ« t in (0 : â„)..s, U_grp.U t Ï† =
                  (âˆ« t in h..(h + s), U_grp.U t Ï†) + âˆ« t in (0 : â„)..h, U_grp.U t Ï† := by
          have eq1 := intervalIntegral.integral_add_adjacent_intervals (hint 0 s) (hint s (s + h))
          have eq2 := intervalIntegral.integral_add_adjacent_intervals (hint 0 h) (hint h (h + s))
          calc (âˆ« t in s..(s + h), U_grp.U t Ï†) + âˆ« t in (0 : â„)..s, U_grp.U t Ï†
              = (âˆ« t in (0 : â„)..s, U_grp.U t Ï†) + âˆ« t in s..(s + h), U_grp.U t Ï† := by abel
            _ = âˆ« t in (0 : â„)..(s + h), U_grp.U t Ï† := eq1
            _ = âˆ« t in (0 : â„)..(h + s), U_grp.U t Ï† := by rw [h3]
            _ = (âˆ« t in (0 : â„)..h, U_grp.U t Ï†) + âˆ« t in h..(h + s), U_grp.U t Ï† := eq2.symm
            _ = (âˆ« t in h..(h + s), U_grp.U t Ï†) + âˆ« t in (0 : â„)..h, U_grp.U t Ï† := by abel
        have h_sub : âˆ€ a b c d : H, a + b = c + d â†’ a - d = c - b := by
          intros a b c d heq
          have h1 : a = c + d - b := by rw [â† heq]; abel
          rw [h1]; abel
        exact h_sub _ _ _ _ key
      rw [h_arith]
      have h_scalar : ((I * s)â»Â¹ : â„‚) * (hâ»Â¹ : â„‚) = ((I * h)â»Â¹ : â„‚) * (sâ»Â¹ : â„‚) := by
        field_simp
      rw [h_scalar, â† smul_smul, smul_sub]
    apply Tendsto.congr'
    Â· filter_upwards [self_mem_nhdsWithin] with s hs
      exact (h_key s hs).symm
    Â· exact Tendsto.smul tendsto_const_nhds (h_FTC2.sub h_FTC1)
  Â· push_neg at hpos
    have hneg : h < 0 := lt_of_le_of_ne hpos (Ne.symm hh.symm)
    have h_empty : Set.Ioc 0 h = âˆ… := Set.Ioc_eq_empty (not_lt.mpr (le_of_lt hneg))
    unfold averagedVector
    rw [h_empty, setIntegral_empty, smul_zero]
    exact (generatorDomain U_grp).zero_mem

theorem generatorDomain_dense_via_average :
    Dense (generatorDomain U_grp : Set H) := by
  rw [Metric.dense_iff]
  intro Ï† Îµ hÎµ
  have h_tendsto := averagedVector_tendsto U_grp Ï†
  rw [Metric.tendsto_nhds] at h_tendsto
  specialize h_tendsto Îµ hÎµ
  rw [Filter.eventually_iff_exists_mem] at h_tendsto
  obtain âŸ¨S, hS_mem, hS_ballâŸ© := h_tendsto
  rw [mem_nhdsWithin] at hS_mem
  obtain âŸ¨U, hU_open, hU_zero, hU_subâŸ© := hS_mem
  rw [Metric.isOpen_iff] at hU_open
  obtain âŸ¨Î´, hÎ´_pos, hÎ´_ballâŸ© := hU_open 0 hU_zero
  have hh : Î´ / 2 â‰  0 := by linarith
  have hh_pos : Î´ / 2 > 0 := by linarith
  refine âŸ¨averagedVector U_grp (Î´ / 2) hh Ï†, ?_, ?_âŸ©
  Â· have h_in_ball : Î´ / 2 âˆˆ Metric.ball 0 Î´ := by
      rw [Metric.mem_ball, Real.dist_0_eq_abs, abs_of_pos hh_pos]
      linarith
    have h_in_U : Î´ / 2 âˆˆ U := hÎ´_ball h_in_ball
    have h_in_S : Î´ / 2 âˆˆ S := hU_sub âŸ¨h_in_U, hh_posâŸ©
    have := hS_ball (Î´ / 2) h_in_S
    rw [dif_pos hh] at this
    exact this
  Â· exact averagedVector_in_domain U_grp (Î´ / 2) hh Ï†

theorem generatorDomain_dense : Dense (generatorDomain U_grp : Set H) :=
  generatorDomain_dense_via_average U_grp

lemma generatorDomain_maximal (Ïˆ : H)
    (h : âˆƒ Î· : H, Tendsto (fun t : â„ => ((I : â„‚) * t)â»Â¹ â€¢ (U_grp.U t Ïˆ - Ïˆ)) (ğ“[â‰ ] 0) (ğ“ Î·)) :
    Ïˆ âˆˆ generatorDomain U_grp := h

/-- The complete generator structure for a strongly continuous one-parameter unitary group. -/
noncomputable def generatorOfUnitaryGroup : Generator U_grp where
  op := generatorOp U_grp
  domain := generatorDomain U_grp
  dense_domain := generatorDomain_dense U_grp
  generator_formula := generator_formula_holds U_grp
  domain_invariant := generatorDomain_invariant U_grp
  symmetric := generator_symmetric U_grp
  domain_maximal := generatorDomain_maximal U_grp

theorem generatorOfUnitaryGroup_isSelfAdjoint :
    (generatorOfUnitaryGroup U_grp).IsSelfAdjoint := by
  constructor
  Â· intro Ï†
    obtain âŸ¨Ïˆ, hÏˆ_eqâŸ© := range_plus_i_eq_top U_grp Ï†
    exact âŸ¨Ïˆ.val, Ïˆ.property, hÏˆ_eqâŸ©
  Â· intro Ï†
    obtain âŸ¨Ïˆ, hÏˆ_eqâŸ© := range_minus_i_eq_top U_grp Ï†
    exact âŸ¨Ïˆ.val, Ïˆ.property, hÏˆ_eqâŸ©

end AveragedVectors



variable (U_grp : OneParameterUnitaryGroup (H := H))

/-- Constructs the unique self-adjoint generator of a strongly continuous
    one-parameter unitary group. This is the forward direction of Stone's theorem. -/
noncomputable def Generator.ofUnitaryGroup
    (U_grp : OneParameterUnitaryGroup (H := H)) :
    Generator U_grp :=
  generatorOfUnitaryGroup U_grp

theorem Generator.ofUnitaryGroup_isSelfAdjoint
    (U_grp : OneParameterUnitaryGroup (H := H)) :
    (Generator.ofUnitaryGroup U_grp).IsSelfAdjoint :=
  generatorOfUnitaryGroup_isSelfAdjoint U_grp

theorem generatorOfUnitaryGroup_eq_ofUnitaryGroup :
    generatorOfUnitaryGroup U_grp = Generator.ofUnitaryGroup U_grp := by
  unfold generatorOfUnitaryGroup Generator.ofUnitaryGroup
  rfl

theorem isSelfAdjoint_transfer :
    (Generator.ofUnitaryGroup U_grp).IsSelfAdjoint := by
  rw [â† generatorOfUnitaryGroup_eq_ofUnitaryGroup]
  exact generatorOfUnitaryGroup_isSelfAdjoint U_grp



end QuantumMechanics.Bochner
